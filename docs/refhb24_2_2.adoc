=== Grundsymbole der Sprache
Die Beschreibungssprache weist die folgenden Symbol-Klassen auf: Namen, Zeichenketten, Zahlen, Erläuterungen, Sonderzeichen, reservierte Wörter und Kommentare.

==== Zeichenvorrat, Zwischenräume und Zeilenenden
Die eigentliche Sprache verwendet nur die druckbaren US-ASCII-Zeichen (32 bis 126). Welche Zeichen nebst dem Leerzeichen als Zwischenräume gelten, ist durch die konkrete Compiler-Implementation festzulegen. Diese legt auch fest, welche Zeichen oder Zeichenkombinationen als Zeilenende gelten. Wie die Zeichen gespeichert werden (Zeichensatz) ist Sache der Implementation des Compilers. Sie kann insbesondere je nach Plattform unterschiedlich sein.

Im Rahmen von Kommentaren dürfen auch weitere Zeichen (z.B. Umlaute und Akzente) vorkommen.

==== Namen
Ein Name ist definiert als eine Folge von maximal 255 Buchstaben, Ziffern und Unterstrichen, wobei das erste Zeichen ein Buchstabe sein muss. Gross- und Kleinbuchstaben werden dabei unterschieden. Namen, die mit reservierten Wörtern der Sprache (vgl. Kapitel 2.2.7 Sonderzeichen und reservierte Wörter) zusammenfallen, sind unzulässig.

++++
<a href="#2_2_C1">&#x203B</a>
++++
[#2_2_C1]
.Syntaxregeln:
----
Name = Letter { Letter | Digit | '_' }.
Letter = ( 'A' | .. | 'Z' | 'a' | .. | 'z' ).
Digit = ( '0' | '1' | .. | '9' ).
HexDigit = ( Digit | 'A' | .. | 'F' | 'a' | .. | 'f' ).
----

Näheres zur Eindeutigkeit und zum Gültigkeitsbereich von Namen ist im Kapitel 2.5.4 Namensräume beschrieben.

==== Zeichenketten
Zeichenketten (Strings) kommen im Zusammenhang mit Konstanten vor. Sie beginnen und enden mit einem Anführungszeichen und dürfen sich nicht über mehrere Zeilen erstrecken. `\"` steht für ein Anführungszeichen und `\\` für einen «Backslash» innerhalb des Strings.

Eine Sequenz von `\u`, unmittelbar gefolgt von exakt vier Hexadezimalziffern, steht für ein beliebiges Unicode-Zeichen. Zeichen ab U+10000 sind wie in der UTF-16-Codierung mit zwei Surrogatcodes zu bezeichnen (siehe https://www.unicode.org).

++++
<a href="#2_2_C2">&#x203B</a>
++++
[#2_2_C2]
.Syntaxregel:
----
String = '"' { <any character except '\' or '"'>
             | '\"'
             | '\\'
             | '\u' HexDigit HexDigit HexDigit HexDigit
             } '"'.
----

==== Zahlen
Zahlen kommen in verschiedener Weise vor: Positive ganze Zahlen inklusive 0 (PosNumber), ganze Zahlen (Number), Dezimalzahlen (Dec) und strukturierte Zahlen. Bei Dezimalzahlen kann die Skalierung als Zehnerpotenz angegeben werden (z.B. 1E2 ist 100, 1E-1 ist 0.1). Strukturierte Zahlen machen nur im Zusammenhang mit entsprechenden Einheiten und Wertebereichen (z.B. Uhrzeit) einen Sinn. Wesentlich ist der Wert der Zahl, nicht deren Darstellung. So ist etwa 007 dieselbe Zahl wie 7.

++++
<a href="#2_2_C3">&#x203B</a>
++++
[#2_2_C3]
.Syntaxregeln:
----
PosNumber = (* Digit *).
Number = [ '+' | '-' ] PosNumber.
Dec = ( Number [ '.' PosNumber ] | Float ).
Float = [ '+' | '-' ] '0.' ( ( '1' | '2' | .. | '9' ) [ PosNumber ]
                             | (* '0' *) ) Scaling.
Scaling = ( 'e' | 'E' ) Number.
----

.Beispiele:
====
----
PosNumber:   5134523              1           23
Number:          123           -435        +5769
Dec:         123.456     0.123456e4    -0.123e-2
Float:         0.1e7   -0.123456E+4   0.987e-100
----
====

==== Eigenschaftsmengen
Für verschiedene Zwecke müssen einem Beschreibungsgegenstand Eigenschaften zugeordnet werden. Dies kann mit einer generellen Syntax erfolgen:

++++
<a href="#2_2_C4">&#x203B</a>
++++
[#2_2_C4]
.Syntaxregel:
----
Properties = [ '(' Property { ',' Property } ')' ].
----

Um zu definieren, dass an einer bestimmten Stelle einer Syntaxregel solche Eigenschaften definiert werden sollen, wird in der Syntax folgendes Konstrukt eingefügt:

----
'Properties' '<' Property-Keyword { ',' Property-Keyword } '>'
----

Man schreibt also "Properties" und definiert in spitzen Klammern die zulässigen Schlüsselwörter. Nimmt man als Beispiel die Regel ClassDef (vgl. Kapitel 2.5.3 Klassen und Strukturen) werden mit `"Properties<ABSTRACT,EXTENDED,FINAL>"` die Schlüsselwörter `"ABSTRACT"`, `"EXTENDED"` und `"FINAL"` für die Beschreibung von Eigenschaften akzeptiert. In einer INTERLIS 2-Definition wären dann unter anderem folgende Definitionen möglich:

----
CLASS A (ABSTRACT) = .....
CLASS A (EXTENDED, FINAL) = .....
----

==== Erläuterungen
Erläuterungen werden dort verlangt, wo ein Sachverhalt näher beschrieben werden muss. Aus der Sicht des Standard-Mechanismus wird die Erläuterung nicht weiter interpretiert, also als Kommentar aufgefasst. Es ist aber durchaus zulässig, Erläuterungen detaillierter zu formalisieren und damit einer weitergehenden maschinellen Verarbeitung zugänglich zu machen. Als Anfang und Abschluss einer Erläuterung wurde `//` gewählt. Innerhalb der Erläuterung dürfen zwei aufeinander folgende Schrägstriche nie vorkommen.

++++
<a href="#2_2_C5">&#x203B</a>
++++
[#2_2_C5]
.Syntaxregel:
----
Explanation = '//' any character except // '//'.
----

==== Sonderzeichen und reservierte Wörter
Sonderzeichen und reservierte Wörter sind in den Syntaxregeln der Sprache (nicht aber bei einer Datenbeschreibung) immer zwischen Apostrophen geschrieben, z.B. `','` oder `'MODEL'`. Die reservierten Wörter werden grundsätzlich mit Grossbuchstaben geschrieben. Konflikte zwischen Namen und reservierten Wörtern sind vermeidbar, wenn Namen nicht ausschliesslich aus Grossbuchstaben bestehen.
Es werden folgende reservierten Wörter verwendet (einige davon wurden in INTERLIS 1 bzw. 2.3 benutzt und bleiben aus Gründen der Kompatibilität reserviert; sie sind nicht fett und kursiv dargestellt):

.Reservierte Wörter in INTERLIS 2.
[frame=none]
[grid=none]
[cols="1m,1m,1m,1m"]
|===
|*ABSTRACT* | *ACCORDING* | *AGGREGATES* | *AGGREGATION*
|*ALL* | *AND* | *ANY* | *ANYCLASS*
|*ANYSTRUCTURE* | *ARCS* | *AREA* | *AS*
|*ASSOCIATION* | *AT* | *ATTRIBUTE* | _ATTRIBUTES_
|*BAG* | *BASE* | *BASED* | *BASKET*
|*BINARY* | *BLACKBOX* | _BLANK_ | *BOOLEAN*
|*BY* | *CARDINALITY* | *CHARSET* | *CIRCULAR*
|*CLASS* | *CLOCKWISE* | _CODE_ | *CONSTRAINT*
|*CONSTRAINTS* | *CONTEXT* | _CONTINUE_ | *CONTINUOUS*
|_CONTOUR_ | *CONTRACTED* | *COORD* | _COORD2_
|_COORD3_ | *COUNTERCLOCKWISE* | *DATE* | *DATETIME*
|_DEFAULT_ | *DEFERRED* | *DEFINED* | _DEGREES_
|*DEPENDS* | _DERIVATIVES_ | *DERIVED* | _DIM1_
|_DIM2_ | *DIRECTED* | *DOMAIN* | *END*
|*ENUMTREEVAL* | *ENUMVAL* | *EQUAL* | *EXISTENCE*
|*EXTENDED* | *EXTENDS* | *EXTERNAL* | *FINAL*
|*FIRST* | _FIX_ | _FONT_ | *FORM*
|*FORMAT* | _FREE_ | *FROM* | *FUNCTION*
|*GENERIC* | *GENERICS* | _GRADS_ | *GRAPHIC*
|*HALIGNMENT* | *HIDING* | _I16_ | _I32_
|_IDENT_ | *IMPORTS* | *IN* | *INHERITANCE*
|*INSPECTION* | *INTERLIS* | *JOIN* | *LAST*
|*LINE* | _LINEATTR_ | _LINESIZE_ | *LIST*
|*LNBASE* | _LOCAL_ | *MANDATORY* | *METAOBJECT*
|*MODEL* | *MTEXT* | *MULTIAREA* | *MULTICOORD*
|*MULTIPOLYLINE* | *MULTISURFACE* | *NAME* | *NO*
|*NOINCREMENTALTRANSFER* | *NOT* | *NULL* | *NUMERIC*
|*OBJECT* | *OBJECTS* | *OF* | *OID*
|*ON* | _OPTIONAL_ | *OR* | *ORDERED*
|*OTHERS* | *OVERLAPS* | *PARAMETER* | *PARENT*
|_PERIPHERY_ | *PI* | *POLYLINE* | *PROJECTION*
|_RADIANS_ | *REFERENCE* | *REFSYS* | *REFSYSTEM*
|*REQUIRED* | *RESTRICTION* | *ROTATION* | *SET*
|*SIGN* | *STRAIGHTS* | *STRUCTURE* | *SUBDIVISION*
|*SURFACE* | *SYMBOLOGY* | _TABLE_ | *TEXT*
|*THATAREA* | *THIS* | *THISAREA* | _TID_
|_TIDSIZE_ | *TIMEOFDAY* | *TO* | *TOPIC*
|_TRANSFER_ | *TRANSIENT* | *TRANSLATION* | *TYPE*
|*UNDEFINED* | *UNION* | *UNIQUE* | *UNIT*
|*UNQUALIFIED* | *URI* | *VALIGNMENT* | *VERSION*
|*VERTEX* | _VERTEXINFO_ | *VIEW* | *WHEN*
|*WHERE* | *WITH* | *WITHOUT* | *XMLNS*
|===

==== Kommentare
Es werden zwei Kommentarformen angeboten:

===== Zeilenkommentar
Ein Zeilenkommentar wird mit zwei Ausrufezeichen eröffnet, die unmittelbar aufeinander folgen. Der Zeilenkommentar wird durch das Zeilenende abgeschlossen.

++++
<a href="#2_2_C6">&#x203B</a>
++++
[#2_2_C6]
.Syntaxregel:
----
!! Line comment; goes until end of line
----

===== Blockkommentar
Der Blockkommentar wird durch einen Schrägstrich und einen Stern eingeleitet; abgeschlossen wird er durch einen Stern und einen Schrägstrich. Er darf sich über mehrere Zeilen hinweg erstrecken und seinerseits Zeilenkommentare enthalten. Geschachtelte Blockkommentare sind zugelassen.

++++
<a href="#2_2_C7">&#x203B</a>
++++
[#2_2_C7]
.Syntaxregel:
----
/* Block comment,
    additional line comment */
----
